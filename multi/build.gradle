plugins {
    id("redsmokes.shadow-module")
}

dependencies {
    compileOnly('com.github.milkbowl:VaultAPI:1.7') {
        exclude group: "org.bukkit", module: "bukkit"
    }
    compileOnly 'net.luckperms:api:5.4'

    api 'io.papermc:paperlib:1.0.6'

    api 'org.bstats:bstats-bukkit:3.0.0'

    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    implementation 'org.checkerframework:checker-qual:3.22.1'

    // Providers
    api project(':providers:BaseProviders')
    api project(':providers:PaperProvider')
    api(project(':providers:NMSReflectionProvider')) {
        exclude group: "org.bukkit", module: "bukkit"
    }
    api(project(':providers:1_8Provider')) {
        exclude group: "org.spigotmc", module: "spigot"
    }
    api(project(':providers:1_12Provider')) {
        exclude group: "org.bukkit", module: "bukkit"
    }
}

shadowJar {
    dependencies {
        include (dependency('io.papermc:paperlib'))
        include (dependency('org.bstats:bstats-bukkit'))
        include (dependency('org.bstats:bstats-base'))
        include (dependency('org.spongepowered:configurate-yaml'))
        include (dependency('org.spongepowered:configurate-core'))
        include (dependency('org.yaml:snakeyaml'))
        include (dependency('io.leangen.geantyref:geantyref'))
        include (dependency('org.checkerframework:checker-qual'))
        include (project(':providers:BaseProviders'))
        include (project(':providers:PaperProvider'))
        include (project(':providers:NMSReflectionProvider'))
        include (project(':providers:1_8Provider'))
        include (project(':providers:1_12Provider'))
    }
    relocate 'io.papermc.lib', 'com.bss.inc.redsmokes.main.libs.paperlib'
    relocate 'org.bstats', 'com.bss.inc.redsmokes.main.libs.bstats'
    relocate 'org.spongepowered.configurate', 'com.bss.inc.redsmokes.main.libs.configurate'
    relocate 'org.yaml.snakeyaml', 'com.bss.inc.redsmokes.main.libs.snakeyaml'
    relocate 'io.leangen.geantyref', 'com.bss.inc.redsmokes.main.libs.geantyref'
    relocate 'org.checkerframework', 'com.bss.inc.redsmokes.main.libs.checkerframework'

    minimize {
        include(dependency('org.checkerframework:checker-qual'))
    }
}

repositories {
    mavenCentral()
}